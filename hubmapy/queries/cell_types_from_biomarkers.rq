PREFIX ccf: <http://purl.org/ccf/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX hgnc: <http://identifiers.org/hgnc/>

SELECT DISTINCT ?cell_type ?cell_type_label ?anatomical_structure ?anatomical_structure_label WHERE {
    ?cell_type rdfs:subClassOf [
        owl:onProperty obo:RO_0015004 ;
        owl:someValuesFrom ?characterizing_biomarker_set ] .
    ?characterizing_biomarker_set owl:intersectionOf ?list .
    FILTER EXISTS {
      ?list rdf:rest*/rdf:first/owl:someValuesFrom ?marker .
      FILTER ( ?marker IN ( ?biomarkers ) )
    } .
    ?cell_type rdfs:label ?cell_type_label .
    OPTIONAL {
        ?cell_type ccf:ccf_located_in ?anatomical_structure.
        ?anatomical_structure rdfs:label ?anatomical_structure_label .
    }
}
GROUP BY ?cell_type ?cell_type_label ?anatomical_structure ?anatomical_structure_label
ORDER BY ?cell_type_label